buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.ajoberstar:gradle-git:0.8.0' }
}

import org.ajoberstar.grgit.*

def grgitserver = Grgit.open('.')
def serverbranch = grgitserver.branch.getCurrent().name
println 'Current kwikshop-server branch: ' + serverbranch

File dir = new File('../kwikshop-common')
if (!dir.exists()) {
    println 'Cloning kwikshop-common repository...'
    def grgit = Grgit.clone(dir: dir, uri: 'https://github.com/FAU-Inf2/kwikshop-common')
    if(serverbranch == 'release' || serverbranch == 'refs/heads/release') {
      grgit.branch.add(name: 'release', startPoint: 'origin/release')
      grgit.checkout(branch: 'release')
    }
} else {
    println 'Pulling changes from kwikshop-common repository...'

    def grgit = Grgit.open(dir)
    def commonbranch = grgit.branch.getCurrent().fullName
    println 'Current kwikshop-common branch: ' + commonbranch

    grgit.pull()
}

include ':server'
include ':kwikshop-common'
project(":kwikshop-common").projectDir = file("../kwikshop-common")
